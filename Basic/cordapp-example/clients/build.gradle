plugins {
    id 'org.springframework.boot'
    id 'java'
}

sourceSets {
    main {
        resources {
            srcDir file("$rootDir/config/dev")
        }
    }
}

dependencies {
    // Corda dependencies.
    implementation "$corda_release_group:corda-rpc:$corda_release_version"
    implementation "net.corda:corda-jackson:$corda_release_version"

    // CorDapp dependencies.
    implementation project(':contracts')
    implementation project(':workflows')
    implementation("org.springframework.boot:spring-boot-starter-websocket:$spring_boot_version") {
        exclude group: "org.springframework.boot", module: "spring-boot-starter-logging"
    }
    runtimeOnly "org.apache.logging.log4j:log4j-slf4j-impl:$log4j_version"
    runtimeOnly "org.apache.logging.log4j:log4j-web:$log4j_version"
}


springBoot {
    mainClassName = "net.corda.samples.example.webserver.ServerKt"
}

/* The Client is the communication channel between the external and the node. This task will help you immediately
 * execute your rpc methods in the main method of the client.kt. You can somewhat see this as a quick test of making
 * RPC calls to your nodes.
 */
tasks.register('runTestClient', JavaExec) {
    dependsOn assemble
    classpath = sourceSets.main.runtimeClasspath
    main = 'net.corda.samples.example.ClientKt'
    args 'localhost:10006', 'user1', 'test'
}

/* This task will start the springboot server that connects to your node (via RPC connection). All of the http requests
 * are in the Controller file. You can leave the Server.kt and NodeRPCConnection.kt file untouched for your use.
 */
tasks.register('runPartyAServer', JavaExec) {
    dependsOn assemble
    classpath = sourceSets.main.runtimeClasspath
    main = 'net.corda.samples.example.webserver.ServerKt'
    args '--server.port=50005', '--config.rpc.host=localhost', '--config.rpc.port=10006', '--config.rpc.username=user1', '--config.rpc.password=test'
}

tasks.register('runPartyBServer', JavaExec) {
    dependsOn assemble
    classpath = sourceSets.main.runtimeClasspath
    main = 'net.corda.samples.example.webserver.ServerKt'
    args '--server.port=50006', '--config.rpc.host=localhost', '--config.rpc.port=10009', '--config.rpc.username=user1', '--config.rpc.password=test'
}
