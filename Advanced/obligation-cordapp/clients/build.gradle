repositories {
    mavenLocal()

    mavenCentral()
    maven { url 'https://dl.bintray.com/kotlin/exposed' }
    maven { url 'https://jitpack.io' }
    maven { url 'https://download.corda.net/maven/corda-releases' }
}

apply plugin: 'kotlin'
apply plugin: 'net.corda.plugins.cordapp'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'net.corda.plugins.cordformation'
apply plugin: 'net.corda.plugins.quasar-utils'

cordapp {
    targetPlatformVersion corda_platform_version.toInteger()
    minimumPlatformVersion corda_platform_version.toInteger()
}

sourceSets {
    main {
        resources {
            srcDir "../config/dev"
        }
    }
}

// See https://docs.spring.io/dependency-management-plugin/docs/current/reference/html
dependencyManagement {
    dependencies {
        dependency "org.apache.logging.log4j:log4j-slf4j2-impl:$log4j_version"
    }
}

configurations {
    all {
        exclude group: 'ch.qos.logback', module: 'logback-classic'
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testImplementation "junit:junit:$junit_version"

    // Corda dependencies.
    implementation "net.corda:corda-core:$corda_release_version"
    implementation "net.corda:corda-jackson:$corda_release_version"
    implementation "net.corda:corda-rpc:$corda_release_version"
    implementation "net.corda:corda:$corda_release_version"
    testImplementation "net.corda:corda-node-driver:$corda_release_version"

    implementation("org.springframework.boot:spring-boot-starter-websocket:$spring_boot_version") {
        exclude group: "org.springframework.boot", module: "spring-boot-starter-logging"
    }
    implementation "org.springframework.boot:spring-boot-starter-log4j2:$spring_boot_version"
    implementation "org.apache.logging.log4j:log4j-web:$log4j_version"

    cordaProvided "$corda_core_release_group:corda-finance-contracts:$corda_release_version"
    cordaProvided "$corda_core_release_group:corda-finance-workflows:$corda_release_version"

    // CorDapp dependencies.
    implementation project(":contracts")
    implementation project(":workflows")

    implementation "org.bouncycastle:bcprov-jdk18on:1.75"
    implementation "org.bouncycastle:bcpkix-jdk18on:1.75"
    implementation "javax.annotation:javax.annotation-api:1.2"
}

task runPartyAServer(type: JavaExec, dependsOn: jar) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'net.corda.samples.obligation.server.Server'
    args '--server.port=10009', '--config.rpc.host=localhost', '--config.rpc.port=10008', '--config.rpc.username=user1', '--config.rpc.password=password'
}

task runPartyBServer(type: JavaExec, dependsOn: jar) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'net.corda.samples.obligation.server.Server'
    args '--server.port=10012', '--config.rpc.host=localhost', '--config.rpc.port=10011', '--config.rpc.username=user1', '--config.rpc.password=password'
}

task runPartyCServer(type: JavaExec, dependsOn: jar) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'net.corda.samples.obligation.server.Server'
    args '--server.port=10015', '--config.rpc.host=localhost', '--config.rpc.port=10014', '--config.rpc.username=user1', '--config.rpc.password=password'
}

jar {
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
}