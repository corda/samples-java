apply plugin: 'org.springframework.boot'

buildscript {
    repositories {
        maven {
            url = uri("https://plugins.gradle.org/m2/")
        }
    }
}

sourceSets {
    main {
        resources {
            srcDir rootProject.file("config/dev")
        }
    }
}

dependencies {
    implementation 'com.google.code.gson:gson:2.8.5'

    // Corda dependencies.
    implementation "$corda_core_release_group:corda-core:$corda_core_release_version"
    implementation "$corda_release_group:corda-rpc:$corda_release_version"

    // CorDapp dependencies.
    implementation project(":contracts")
    implementation project(":workflows")

    implementation("org.springframework.boot:spring-boot-starter-websocket:$spring_boot_version") {
        exclude group: "org.springframework.boot", module: "spring-boot-starter-logging"
    }
    implementation "org.apache.logging.log4j:log4j-slf4j2-impl:${log4j_version}"
    implementation "org.apache.logging.log4j:log4j-web:${log4j_version}"
    implementation "org.slf4j:jul-to-slf4j:$slf4j_version"

    // SendGrid
    implementation 'com.sendgrid:sendgrid-java:4.7.0'
}

springBoot {
    mainClass = "net.corda.samples.secretsanta.webserver.Starter"
}

/* The Client is the communication channel between the external and the node. This task will help you immediately
 * execute your rpc methods in the main method of the client.kt. You can somewhat see this as a quick test of making
 * RPC calls to your nodes.
 */
task runSantaClient(type: JavaExec, dependsOn: assemble) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'net.corda.samples.secretsanta.Client'
    args 'localhost:10016', 'user1', 'test'
}

/**
 * This task will start the springboot server that connects to the santa node (via RPC connection). All of the http requests
 * are in the Controller file.
 *
 */
task runSantaServer(type: JavaExec, dependsOn: assemble) {
    classpath = sourceSets.main.runtimeClasspath
    main = 'net.corda.samples.secretsanta.webserver.Starter'
    args '--server.port=10056', '--config.rpc.host=localhost', '--config.rpc.port=10016', '--config.rpc.username=user1', '--config.rpc.password=test'
}